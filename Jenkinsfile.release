@Library('Utils') _

pipeline {
    agent any
    stages {
        stage('Create Credentials') {
            steps {
                checkout scm
                // Create a Jenkins Credential from OpenShift Secret
                // In this case the OpenShift service tokens for the
                // other environments.
                syncOpenShiftSecret "${params.PROD_SECRET_NAME}"
                syncOpenShiftSecret "${params.REGISTRY_SECRET_NAME}"
            }
        }
        stage('Production - Tag') {
            environment {
                REGISTRY = credentials("${params.REGISTRY_SECRET_NAME}")
                RELEASE_VERSION = readFile('app/VERSION').trim()
            }
            steps {
                script {
                    def userInput
                    timeout(2) {
                        userInput = input(message: "Enter build to release as version ${env.RELEASE_VERSION}", parameters: [string(defaultValue: '', description: 'Build version image tag, e.g. 1.3-8', name: 'buildVersion')])
                    }
                    echo "Releasing build ${userInput} as ${env.RELEASE_VERSION}"
                    openshift.withCluster("${params.OPENSHIFT_REGISTRY_URI}", env.REGISTRY_PSW) {
                        openshift.withProject("${params.REGISTRY_PROJECT}") {
                            def istagSelector = openshift.selector("istag", "${params.IMAGE_STREAM_NAME}:${userInput}")
                            if (!istagSelector.exists()) {
                                echo "Imagestream tag ${userInput} does not exist in registry"
                                currentBuild.result = 'ABORTED'
                            }
                        }
                    }

                    openshift.withCluster("${params.OPENSHIFT_REGISTRY_URI}", env.REGISTRY_PSW) {
                        openshift.withProject("${params.REGISTRY_PROJECT}") {
                            openshift.tag("${openshift.project()}/${params.IMAGE_STREAM_NAME}:${userInput}",
                                    "${openshift.project()}/${params.IMAGE_STREAM_NAME}:${env.RELEASE_VERSION}")
                            openshift.tag("${openshift.project()}/${params.IMAGE_STREAM_NAME}:${userInput}",
                                    "${openshift.project()}/${params.IMAGE_STREAM_NAME}:latest")
                        }
                    }
                }
            }
        }
        stage('Production - OpenShift Template') {
            environment {
                PRODUCTION = credentials("${params.PROD_SECRET_NAME}")
                RELEASE_VERSION = readFile('app/VERSION').trim()
            }
            steps {
                script {
                    openshift.withCluster("${params.PROD_URI}", env.PRODUCTION_PSW) {
                        openshift.withProject("${params.PROD_PROJECT}") {
                            // Apply the template object from JSON file
                            openshift.apply(readFile("${params.APP_TEMPLATE_PATH}"))

                            createdObjects = openshift.apply(
                                    openshift.process("${params.IMAGE_STREAM_NAME}",
                                            "-p",
                                            "TAG=${env.RELEASE_VERSION}",
                                            "IMAGESTREAM_TAG=${env.RELEASE_VERSION}",
                                            "REGISTRY=${params.REGISTRY_URI}",
                                            "PROJECT=${params.PROD_PROJECT}"))

                            // The production environment does not need buildconfigs
                            createdObjects.narrow('bc').delete()
                        }
                    }
                }
            }
        }
        stage('Production - Rollout') {
            environment {
                PRODUCTION = credentials("${params.PROD_SECRET_NAME}")
            }
            steps {
                script {
                    openshift.withCluster("${params.PROD_URI}", env.PRODUCTION_PSW) {
                        openshift.withProject("${params.PROD_PROJECT}") {
                            deploymentConfigs = createdObjects.narrow('dc')
                            nodedc = deploymentConfigs.selector("dc", "${params.APP_DC_NAME}")
                            def rolloutManager = nodedc.rollout()
                            rolloutManager.latest()

                            timeout(10) {
                                nodedc.rollout().status("-w")
                            }
                        }
                    }
                }
            }
        }
    }
    post {
        success {
            mail to: "${params.NOTIFY_EMAIL_LIST}",
            from: 'jenkins@example.com', replyTo: 'noreply@example.com',
            subject: "${params.IMAGE_STREAM_NAME} version ${env.RELEASE_VERSION} released to production",
            body: "Visit ${BUILD_URL} for details."
        }
        failure {
            mail to: "${params.NOTIFY_EMAIL_LIST}",
            from: 'jenkins@example.com', replyTo: 'noreply@example.com',
            subject: "Failed production release of ${params.IMAGE_STREAM_NAME} version ${env.RELEASE_VERSION}",
            body: "Visit ${BUILD_URL} for details."
        }
    }
}

// vim: ft=groovy
